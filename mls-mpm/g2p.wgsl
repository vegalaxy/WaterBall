struct Particle {
    position: vec3f, 
    v: vec3f, 
    C: mat3x3f, 
}
struct Cell {
    vx: i32, 
    vy: i32, 
    vz: i32, 
    mass: i32, 
}

override fixed_point_multiplier: f32; 
override dt: f32; 

@group(0) @binding(0) var<storage, read_write> particles: array<Particle>;
@group(0) @binding(1) var<storage, read> cells: array<Cell>;
@group(0) @binding(2) var<uniform> real_box_size: vec3f;
@group(0) @binding(3) var<uniform> init_box_size: vec3f;
@group(0) @binding(4) var<uniform> numParticles: u32;
@group(0) @binding(5) var<uniform> sphereRadius: f32;

fn decodeFixedPoint(fixed_point: i32) -> f32 {
	return f32(fixed_point) / fixed_point_multiplier;
}


@compute @workgroup_size(64)
fn g2p(@builtin(global_invocation_id) id: vec3<u32>) {
    if (id.x < numParticles) {
        particles[id.x].v = vec3f(0.);
        var weights: array<vec3f, 3>;

        let particle = particles[id.x];
        let cell_idx: vec3f = floor(particle.position);
        let cell_diff: vec3f = particle.position - (cell_idx + 0.5f);
        weights[0] = 0.5f * (0.5f - cell_diff) * (0.5f - cell_diff);
        weights[1] = 0.75f - cell_diff * cell_diff;
        weights[2] = 0.5f * (0.5f + cell_diff) * (0.5f + cell_diff);

        var B: mat3x3f = mat3x3f(vec3f(0.), vec3f(0.), vec3f(0.));
        for (var gx = 0; gx < 3; gx++) {
            for (var gy = 0; gy < 3; gy++) {
                for (var gz = 0; gz < 3; gz++) {
                    let weight: f32 = weights[gx].x * weights[gy].y * weights[gz].z;
                    let cell_x: vec3f = vec3f(
                        cell_idx.x + f32(gx) - 1., 
                        cell_idx.y + f32(gy) - 1.,
                        cell_idx.z + f32(gz) - 1.  
                    );
                    let cell_dist: vec3f = (cell_x + 0.5f) - particle.position;
                    let cell_index: i32 = 
                        i32(cell_x.x) * i32(init_box_size.y) * i32(init_box_size.z) + 
                        i32(cell_x.y) * i32(init_box_size.z) + 
                        i32(cell_x.z);
                    let weighted_velocity: vec3f = vec3f(
                        decodeFixedPoint(cells[cell_index].vx), 
                        decodeFixedPoint(cells[cell_index].vy), 
                        decodeFixedPoint(cells[cell_index].vz)
                    ) * weight;
                    let term: mat3x3f = mat3x3f(
                        weighted_velocity * cell_dist.x, 
                        weighted_velocity * cell_dist.y, 
                        weighted_velocity * cell_dist.z
                    );

                    B += term;

                    particles[id.x].v += weighted_velocity;
                }
            }
        }

        particles[id.x].C = B * 4.0f;
        particles[id.x].position += particles[id.x].v * dt;

        let tube_radius: f32 = 15.0;
        let center_y: f32 = real_box_size.y / 2.0;
        let center_z: f32 = real_box_size.z / 2.0;
        let wave_center_x: f32 = real_box_size.x * 0.5;
        let pos = particles[id.x].position;

        let radial_dist = sqrt((pos.y - center_y) * (pos.y - center_y) + (pos.z - center_z) * (pos.z - center_z));

        let flow_force: f32 = 3.5;
        particles[id.x].v.x += flow_force * dt;

        let wave_start: f32 = wave_center_x - 20.0;
        let wave_peak: f32 = wave_center_x;
        let wave_end: f32 = wave_center_x + 15.0;

        if (pos.x >= wave_start && pos.x <= wave_peak) {
            let wave_progress = (pos.x - wave_start) / (wave_peak - wave_start);
            let lift_force = 10.0 * wave_progress;
            particles[id.x].v.y += lift_force * dt;
        }

        if (pos.x >= wave_peak && pos.x <= wave_end) {
            let curl_progress = (pos.x - wave_peak) / (wave_end - wave_peak);
            let wave_height = center_y + 10.0;

            if (pos.y > wave_height) {
                let curl_force = 4.0;
                particles[id.x].v.x -= curl_force * curl_progress * dt;
                particles[id.x].v.y -= curl_force * 1.8 * dt;

                let barrel_radius = 9.0;
                let angle = atan2(pos.z - center_z, pos.y - center_y);
                let target_angle = angle + 3.14159 * 0.25;
                let target_y = center_y + barrel_radius * cos(target_angle);
                let target_z = center_z + barrel_radius * sin(target_angle);

                particles[id.x].v.y += (target_y - pos.y) * 0.4 * dt;
                particles[id.x].v.z += (target_z - pos.z) * 0.4 * dt;
            }
        }

        if (radial_dist > tube_radius) {
            let radial_dir_y = (center_y - pos.y) / radial_dist;
            let radial_dir_z = (center_z - pos.z) / radial_dist;
            let confinement_force = 6.0;
            particles[id.x].v.y += radial_dir_y * confinement_force * dt;
            particles[id.x].v.z += radial_dir_z * confinement_force * dt;
        }

        if (pos.x > real_box_size.x - 8.0) {
            particles[id.x].position.x = 10.0 + fract(sin(f32(id.x) * 12.9898) * 43758.5453) * 2.0;
            let spawn_radius = tube_radius * 0.6;
            let spawn_angle = fract(sin(f32(id.x) * 78.233) * 43758.5453) * 6.28318;
            particles[id.x].position.y = center_y + spawn_radius * cos(spawn_angle);
            particles[id.x].position.z = center_z + spawn_radius * sin(spawn_angle);
            particles[id.x].v = vec3f(12.0, 0.0, 0.0);
            particles[id.x].C = mat3x3f(vec3f(0.), vec3f(0.), vec3f(0.));
        }

        particles[id.x].position = vec3f(
            clamp(particles[id.x].position.x, 1., real_box_size.x - 2.),
            clamp(particles[id.x].position.y, 1., real_box_size.y - 2.),
            clamp(particles[id.x].position.z, 1., real_box_size.z - 2.)
        );

        let k = 3.0;
        let wall_stiffness = 1.0;
        let x_n: vec3f = particles[id.x].position + particles[id.x].v * dt * k;
        let wall_min: vec3f = vec3f(3.);
        let wall_max: vec3f = real_box_size - 4.;
        if (x_n.y < wall_min.y) { particles[id.x].v.y += wall_stiffness * (wall_min.y - x_n.y); }
        if (x_n.y > wall_max.y) { particles[id.x].v.y += wall_stiffness * (wall_max.y - x_n.y); }
        if (x_n.z < wall_min.z) { particles[id.x].v.z += wall_stiffness * (wall_min.z - x_n.z); }
        if (x_n.z > wall_max.z) { particles[id.x].v.z += wall_stiffness * (wall_max.z - x_n.z); }
    }
}